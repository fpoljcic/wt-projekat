@objects
	meni 		css     .meni
	meni_item_1	xpath	//div[@class='meni']/ul/li[1]
	meni_item_2	xpath	//div[@class='meni']/ul/li[2]
	logo 		css     .logo
	ul 			css		.meni ul
	sadrzaj 	css		.sadrzaj
	tabela 		xpath	//div[@class='kalendar']/table
	labela_1	xpath	//div[@class='sadrzaj']/label[1]
	select_1	xpath	//div[@class='sadrzaj']/select
	labela_2	xpath	//div[@class='sadrzaj']/label[2]
	button		css 	input[type=button] 
= Meni section =
	logo:
		inside partly meni 40px bottom
		width 97 to 99% of viewport/width
		height 80 to 84px
		centered horizontally inside viewport
	meni:
		above labela_1 12 to 16px
		width 97 to 99% of viewport/width
		centered horizontally inside viewport
	ul:
		inside meni
		contains meni_item_1
		inside viewport 7 to 11px left
		below logo
		centered horizontally inside meni 1px
	meni_item_1:
		aligned horizontally all meni_item_2
		near meni_item_2 2 to 6px left
		height 12 to 16px
		inside ul 0 to 2px bottom, 0px top
	meni_item_2:
		inside ul 0 to 2px bottom, 0px top
		height 12 to 16px
= Main section =
	sadrzaj:
		width 97 to 99% of viewport/width
		below meni
		centered horizontally inside viewport
	tabela:
		below sadrzaj 3 to 7px
		width 49 to 51% of viewport/width
	labela_1:
		below meni 10 to 16px
		inside viewport 6 to 10px left
	select_1:
		right-of labela_1 3 to 7px
		aligned horizontally centered labela_1 1px
		below meni 6 to 12px
	labela_2:
		aligned horizontally centered labela_1
		right-of select_1 2 to 6px
		height 100% of labela_1/height
	button:
		below tabela 5 to 7px
		aligned vertically left tabela